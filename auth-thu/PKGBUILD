pkgname=auth-thu
_pkgname=GoAuthing
pkgver=v2.2.1.r7.gd828582
pkgrel=1
pkgdesc="A commandline Tunet (auth4/6.tsinghua.edu.cn, Tsinghua-IPv4) authentication tool"
arch=('x86_64')
url="https://github.com/z4yx/GoAuthing"
license=('GPL3')
makedepends=('go')
source=("git+${url}.git")
sha256sums=('SKIP')

pkgver() {
    cd "${srcdir}"/"${_pkgname}"/
    git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cd "${srcdir}"/"${_pkgname}"/
    CGO_ENABLED=0 go build -ldflags="-s -w" -o auth-thu ./cli
}

package() {
    cd "${srcdir}"/"${_pkgname}"/
    install -Dm755 "auth-thu" "$pkgdir/usr/bin/auth-thu"
    install -Dm644 "LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
    cd "docs/systemd/system"
    sed -i "s|/usr/local/bin/auth-thu|/usr/bin/auth-thu|g" goauthing*.service
    install -Dm644 goauthing*.service -t "$pkgdir/usr/lib/systemd/system"
}
