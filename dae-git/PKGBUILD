_pkgname=dae
pkgname=$_pkgname-git
pkgver=0.6.0rc1.r5.g89a0596
pkgrel=1
pkgdesc="A Linux lightweight and high-performance transparent proxy solution based on eBPF."
arch=(x86_64)
url="https://github.com/daeuniverse/dae"
license=('AGPL-3.0')
makedepends=(clang llvm go)
provides=($_pkgname)
conflicts=($_pkgname)
depends=('glibc')
source=("git+${url}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${srcdir}"/"${_pkgname}"/
	git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd "${srcdir}"/"${_pkgname}"/
	git submodule update --init
	export GOFLAGS="-buildmode=pie -trimpath -modcacherw"
	export CFLAGS="-fno-stack-protector"
	make
}

package() {
	depends=(dae-rules-dat)
	cd "$srcdir/$_pkgname"
	install -Dm755 "dae" "$pkgdir/usr/bin/dae"
	install -Dm644 "install/dae.service" "$pkgdir/usr/lib/systemd/system/dae.service"
}
